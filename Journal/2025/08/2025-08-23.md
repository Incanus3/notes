### Elixir
#### schema (json) validation using ecto

```elixir
Mix.install([:ecto])

defmodule CartItem do
  use Ecto.Schema
  import Ecto.Changeset
  @demo_payload %{
      "user_id" => "u_123",
      "sku" => "ABC-42",
      "qty" => "3",
      "price" => "12.34",
      "tags" => ["promo", "summer"],
      "status" => "confirmed"
    }

  embedded_schema do
    field(:user_id, :string)
    field(:sku, :string)
    field(:qty, :integer)
    field(:price, :decimal)
    field(:tags, {:array, :string}, default: [])
    field(:status, Ecto.Enum, values: [:pending, :confirmed, :canceled], default: :pending)
  end

  def changeset(struct \\ %__MODULE__{}, params) do
    struct
    |> cast(params, [:user_id, :sku, :qty, :price, :tags, :status])
    |> validate_required([:user_id, :sku, :qty, :price])
    |> validate_number(:qty, greater_than: 0)
    |> validate_number(:price, greater_than: 0)
  end

  def demo() do
    changeset = CartItem.changeset(@demo_payload)
    
    apply_action(changeset, :insert)
  end
  
  def invalid_demo() do
    CartItem.changeset(Map.put(@demo_payload, "status", "not a real status"))
    |> apply_action(:insert)
  end
end
```